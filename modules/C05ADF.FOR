
      SUBROUTINE C05ADF(A, B, EPS, ETA, F, X, IFAIL)
C     MARK 8 RELEASE. NAG COPYRIGHT 1979.
C     .. SCALAR ARGUMENTS ..
      DOUBLE PRECISION A, B, EPS, ETA, X
      INTEGER IFAIL
C     .. FUNCTION ARGUMENTS ..
      DOUBLE PRECISION F
C     ..
C     .. LOCAL SCALARS ..
C$P 1
      DOUBLE PRECISION SRNAME
      DOUBLE PRECISION FX, Y
      INTEGER IFAIL1, IND, IR
C     .. LOCAL ARRAYS ..
      DOUBLE PRECISION C(17)
C     .. FUNCTION REFERENCES ..
      INTEGER P01AAF
C     .. SUBROUTINE REFERENCES ..
C     C05AZF
C     ..
      DATA SRNAME /8H C05ADF /
C     DRIVER FOR C05AZF
      IFAIL1 = 1
C     INPUT ERROR
      IF (A.EQ.B .OR. EPS.LE.0.D0) GO TO 120
      X = A
      FX = F(X)
      IFAIL1 = 0
C     ZERO AT INITIAL POINT
      IF (DABS(FX).LT.ETA .OR. FX.EQ.0.D0) GO TO 120
      Y = X
      C(1) = FX
      X = B
      FX = F(X)
C     ZERO AT INITIAL POINT
      IF (DABS(FX).LT.ETA .OR. FX.EQ.0.D0) GO TO 120
      IFAIL1 = 1
C     NO ROOT IN RANGE
      IF (DSIGN(1.D0,FX).EQ.DSIGN(1.D0,C(1))) GO TO 120
      IR = 0
      IND = -1
   20 CALL C05AZF(X, Y, FX, EPS, IR, C, IND, IFAIL1)
      IF (IND.EQ.0) GO TO 40
      FX = F(X)
      IF (DABS(FX).GE.ETA .AND. FX.NE.0.D0) GO TO 20
C     ZERO HIT EXACTLY
      IFAIL1 = 0
      GO TO 120
   40 IF (IFAIL1.EQ.0) GO TO 120
      GO TO (60, 60, 60, 100, 80), IFAIL1
C     IMPOSSIBLE EXIT
   60 IFAIL1 = 4
      GO TO 120
C     TOO MUCH ACCURACY REQUESTED
   80 IFAIL1 = 2
      GO TO 120
C     PROBABLY A POLE
  100 IFAIL1 = 3
  120 IFAIL = P01AAF(IFAIL,IFAIL1,SRNAME)
      RETURN
      END
